##########################################################################################
#
# UsePetalinux
#
# This package assumes that find_package(Petalinux) has been called.
# It contains the following functions:
#
##########################################################################################
#
# Function: petalinux_create
#
# Parameters:
#   - PROJ_NAME          Project name (also used as CMake target name)
#   - HW_FILE            Input hardware file (XSA)
#   - CONFIG_MENU        ON --> show config menus (default is OFF)
#   - CONFIG_SRC         Source config
#   - BSP_CFG_SRC        Source bsp.cfg
#   - DEVICE_TREE_FILES  List of device tree files
#   - RECIPES_CORE_SRC_DIR  Directory for recipes-core (optional)
#
# Description:
#   This function creates the petalinux project. The CONFIG_SRC file (config) is copied to
#   the project-spec/configs directory in the build tree and the BSP_CFG_SRC (bsp.cfg) file
#   is copied to the project-spec/meta-user/recipes-kernel/linux/linux-xlnx directory in
#   the build tree.
#
#   The bsp.cfg file contains custom kernel configuration entries that are not stored
#   in "config" or "rootfs_config", but rather in generated files named "user_<date>.cfg"
#   in the project-spec/meta-user/recipes-kernel/linux/linux-xlnx directory.
#   It is easier, however, to put them in the existing "bsp.cfg" file, which
#   is empty by default.
#
#   To change the configuration, set CONFIG_MENU ON via CMake. This will cause
#   the system to show the configuration menus and update the files in the
#   project-spec/configs and project-spec/meta-user/recipes-kernel/linux/linux-xlnx
#   directories in the build tree.
#   Note that this function only updates the "config" and "bsp.cfg" files;
#   the "rootfs_config" is updated in petalinux_app_create and petalinux_build.
#
#   The RECIPES_CORE_SRC_DIR parameter is optional. If specified, the entire directory is
#   copied from the source tree to the build tree. One limitation of the current implementation
#   is that "cmake -E copy_directory" updates the file times and thus we do not have a dependency
#   on modifications to the source files. This means that if any of the recipes-core files
#   are changed, the easiest solution is to delete the file config.cfg in the build tree,
#   which will force the directory to be copied.
#
##########################################################################################
#
# Function: petalinux_app_create
#
# Parameters:
#   - TARGET_NAME        CMake target name (optional, defaults to APP_NAME)
#   - APP_NAME           Application name (also used as CMake target name)
#   - PROJ_NAME          Project name
#   - APP_TEMPLATE       Application template (c, c++, autoconf, fpgamanager, install)
#   - APP_SOURCES        List of source files
#   - APP_BB             Application bb or bbappend file (optional)
#
# Description:
#   This function creates the application and then overwrites it with the specified
#   source files. The APP_TEMPLATE parameter is optional (defaults to c).
#
#   The optional APP_BB parameter can be used to specify a "bb" or "bbappend" file.
#   The "bb" file would replace the autogenerated version, so is useful in cases where the
#   autogenerated file is not relevant (e.g., when using petalinux_app_create to create a
#   library). For other scenarios, it is better to specify a "bbappend" file, which would
#   add to the information in the autogenerated "bb" file.
#
##########################################################################################
#
# Function: petalinux_build
#
# Parameters:
#   - TARGET_NAME        Target name (for CMake)
#   - PROJ_NAME          Project name
#   - CONFIG_MENU        ON --> show config menus (default is OFF)
#   - ROOTFS_CONFIG_SRC  Source rootfs_config
#   - BIT_FILE           BIT file to use for boot image (optional)
#   - FSBL_FILE          FSBL file (elf) to use for boot image (optional, default is
#                        zynq_fsbl.elf in petalinux image directory)
#   - DEPENDENCIES       Build dependencies
#
# Description:
#
#  This function first configures the rootfs, because that needs to be done after any
#  apps are created (via petalinux_app_create).
#  It then builds and packages the petalinux image. The dependency on ${PROJ_NAME}
#  (built by petalinux_create) is already incorporated, so the DEPENDENCIES parameter
#  is used to specify additional dependencies, such as any apps that were created.
#
##########################################################################################
#
# Function: petalinux_build_sdk
#
# Parameters:
#   - TARGET_NAME        CMake target for building SDK
#   - PROJ_NAME          Project name
#   - SDK_INSTALL_DIR    Directory for SDK installation
#   - SYSROOT_ZIP        Sysroot ZIP file name
#   - DEPENDENCIES       Build dependencies
#
# Description:
#
#   This function creates an SDK that can be used to build Petalinux applications in Vitis.
#   It also creates a ZIP file (SYSROOT_ZIP) of the sysroot for the Zynq target -- this sysroot
#   is reduced in size by removing a large number of debug files.
#
#   To use the SDK/sysroot, create a Linux platform in Vitis and then set the SYSROOT to either:
#     1) the appropriate subdirectory in the SDK (i.e., sysroots/cortexa9t2hf-neon-xilinx-linux-gnueabi)
#   or
#     2) the directory in which SYSROOT_ZIP has been unzipped
#
#   The second option is provided for environments where Petalinux is not built (for example, on Windows)
#
##########################################################################################
#
# Important CMake Information
#
# This implementation relies on CMake add_custom_command and add_custom_target, so it is
# important to understand how dependency checking works when using these commands.
#
# We use add_custom_command to produce an output, which is usually one or more files.
# For dependencies (DEPENDS), we can specify files or targets. If a file is specified,
# the command is executed if any of the outputs are older than the file.
# If a target is specified, the custom command will be considered after the target is built
# (i.e., the target dependency controls the order of the build, but may not cause the command
# to be executed). Whether or not the command is executed depends only on the file dependency
# check. Note: although not relevant here, CMake automatically adds file dependencies for
# targets created by add_executable or add_library, but not for add_custom_target.
#
# CMake add_custom_target creates a target that is always executed. Although it is possible
# to specify one or more commands (COMMAND), that capability is not used in this implementation.
# Instead, add_custom_target is just used to control the order of the build process.
# Typically, the dependencies (DEPENDS) are on the output files created by add_custom_command,
# so that these custom commands are executed if needed (based on the dependencies specified
# in add_custom_command). Starting with CMake 3.16, it also adds a target-level dependency
# for any targets in the same directory that create one of the dependent files.
#
# This implementation creates the following targets:
#
#    ${PROJ_NAME}    Petalinux project name, created by petalinux_create. There is only one such
#                    target and it should be the first one built.
#
#    ${APP_NAME}     Application name, created by petalinux_app_create. There may be multiple app
#                    targets as a result of multiple calls to petalinux_app_create. This has a
#                    target-level dependency on ${PROJ_NAME} via one of the dependent custom commands.
#
#    ${TARGET_NAME}  Final target (typically ${PROJ_NAME}_build), created by petalinux_build.
#                    There is only one such target and it has target-level dependencies on ${PROJ_NAME}
#                    and (via DEPENDENCIES arg to petalinux_build) on any ${APP_NAME} targets.
#                    It should be the last target built.
#
################################ petalinux_create ########################################

function (petalinux_create ...)

  # set all keywords and their values to ""
  set (FUNCTION_KEYWORDS
       PROJ_NAME
       HW_FILE
       CONFIG_MENU
       CONFIG_SRC
       BSP_CFG_SRC
       DEVICE_TREE_FILES
       RECIPES_CORE_SRC_DIR)

  # reset local variables
  foreach(keyword ${FUNCTION_KEYWORDS})
    set (${keyword} "")
  endforeach(keyword)
  set (CONFIG_MENU OFF)

  # parse input (could instead use cmake_parse_arguments)
  foreach (arg ${ARGV})
    list (FIND FUNCTION_KEYWORDS ${arg} ARGUMENT_IS_A_KEYWORD)
    if (${ARGUMENT_IS_A_KEYWORD} GREATER -1)
      set (CURRENT_PARAMETER ${arg})
      set (${CURRENT_PARAMETER} "")
    else (${ARGUMENT_IS_A_KEYWORD} GREATER -1)
      set (${CURRENT_PARAMETER} ${${CURRENT_PARAMETER}} ${arg})
    endif (${ARGUMENT_IS_A_KEYWORD} GREATER -1)
  endforeach (arg)

  if (PROJ_NAME AND HW_FILE AND CONFIG_SRC AND BSP_CFG_SRC AND DEVICE_TREE_FILES)

    if (CONFIG_MENU)
      set (CONFIG_OPTION "")
    else ()
      set (CONFIG_OPTION "--silentconfig")
    endif ()

    get_filename_component (HW_FILE_NAME ${HW_FILE} NAME)

    set (DEVICE_TREE_BIN_DIR "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}/project-spec/meta-user/recipes-bsp/device-tree/files")

    set (CONFIG_ARCHIVE_DIR "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}-configs")
    set (README_FILE "${CONFIG_ARCHIVE_DIR}/ReadMe.txt")
    file (WRITE  ${README_FILE} "Directory created by CMake for config files (config and rootfs_config):\n")
    file (APPEND ${README_FILE} " - config.default, rootfs_config.default: original versions from petalinux-create\n")
    file (APPEND ${README_FILE} " - config.hw: config file after specifying the hardware description\n")
    file (APPEND ${README_FILE} " - rootfs_config.<appname>: rootfs_config file after creating app <appname>\n")
    file (APPEND ${README_FILE} " - config.cfg: config file after configuring the kernel (also used as CMake build output)\n")
    file (APPEND ${README_FILE} " - rootfs_config.cfg: config file after configuring rootfs (also used as CMake build output)\n")

    set (CONFIG_BIN_DIR   "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}/project-spec/configs")
    set (CONFIG_BIN_FILE  "${CONFIG_BIN_DIR}/config")
    set (KERNEL_CFG_DIR   "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}/project-spec/meta-user/recipes-kernel/linux/linux-xlnx")

    # Output of petalinux-create -- we rely on two files that we create
    set (PETALINUX_CREATE_OUTPUT "${CONFIG_ARCHIVE_DIR}/config.default" "${CONFIG_ARCHIVE_DIR}/rootfs_config.default")

    add_custom_command (
        OUTPUT ${PETALINUX_CREATE_OUTPUT}
        # Create the project. This does not take very long.
        COMMAND petalinux-create -t project --force --template zynq -n ${PROJ_NAME}
        # Archive default versions of config and rootfs_config
        COMMAND ${CMAKE_COMMAND}
                ARGS -E copy_if_different
                "${CONFIG_BIN_DIR}/config"
                "${CONFIG_ARCHIVE_DIR}/config.default"
        COMMAND ${CMAKE_COMMAND}
                ARGS -E copy_if_different
                "${CONFIG_BIN_DIR}/rootfs_config"
                "${CONFIG_ARCHIVE_DIR}/rootfs_config.default"
        COMMENT "Creating Petalinux project ${PROJ_NAME}")

    # Output of petalinux-config (hardware)
    set (PETALINUX_CONFIG_HW_OUTPUT "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}/project-spec/hw-description/system.xsa")

    add_custom_command (
        OUTPUT ${PETALINUX_CONFIG_HW_OUTPUT}
        # Copy the config file from the source tree (rootfs_config is copied in petalinux_build)
        COMMAND ${CMAKE_COMMAND}
                ARGS -E copy_if_different
                ${CONFIG_SRC}
                ${CONFIG_BIN_FILE}
        # Specify the hardware description (XSA) file. This does not take very long and has one config menu
        # (shown if CONFIG_MENU is ON). This menu can also be shown by typing petalinux-config -p ${PROJ_NAME}
        # on the command line.
        COMMAND petalinux-config -p ${PROJ_NAME} --get-hw-description ${HW_FILE} ${CONFIG_OPTION}
        # Archive config file
        COMMAND ${CMAKE_COMMAND}
                ARGS -E copy_if_different
                "${CONFIG_BIN_DIR}/config"
                "${CONFIG_ARCHIVE_DIR}/config.hw"
        COMMENT "Configuring hardware from ${HW_FILE_NAME}"
        # Adding dependency on ${CONFIG_SRC} forces a complete rebuild if the config file is
        # changed, even though in many cases it would not be necessary (i.e., it is only necessary
        # if one of the hw-description entries is updated).
        DEPENDS ${PETALINUX_CREATE_OUTPUT} ${HW_FILE} ${CONFIG_SRC})

    set (RECIPES_CORE_BIN_DIR "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}/project-spec/meta-user/recipes-core")

    if (RECIPES_CORE_SRC_DIR)
        set (RECIPES_CORE_CMD "copy_directory")
    else ()
        set (RECIPES_CORE_CMD "true")
    endif()

    # Output from configuring kernel
    set (PETALINUX_CONFIG_OUTPUT "${CONFIG_ARCHIVE_DIR}/config.cfg")

    add_custom_command (
        OUTPUT ${PETALINUX_CONFIG_OUTPUT}
        # Copy the bsp.cfg file
        COMMAND ${CMAKE_COMMAND}
                  ARGS -E copy_if_different
                  ${BSP_CFG_SRC}
                  ${KERNEL_CFG_DIR}
        # Copy the recipes-core directory (if specified above)
        COMMAND ${CMAKE_COMMAND}
                ARGS -E ${RECIPES_CORE_CMD}
                ${RECIPES_CORE_SRC_DIR}
                ${RECIPES_CORE_BIN_DIR}
        # Copy the device-tree file if needed
        COMMAND ${CMAKE_COMMAND}
                ARGS -E copy_if_different
                ${DEVICE_TREE_FILES}
                ${DEVICE_TREE_BIN_DIR}
        # Configure the kernel. This takes a long time and has one config menu
        # (shown if CONFIG_MENU is ON).
        COMMAND petalinux-config -p ${PROJ_NAME} -c kernel ${CONFIG_OPTION}
        # Archive the config file (this is also the command output)
        COMMAND ${CMAKE_COMMAND}
                ARGS -E copy
                ${CONFIG_BIN_FILE}
                ${PETALINUX_CONFIG_OUTPUT}
        COMMENT "Configuring Petalinux kernel"
        DEPENDS ${PETALINUX_CONFIG_HW_OUTPUT} ${BSP_CFG_SRC} ${DEVICE_TREE_FILES})

    add_custom_target (${PROJ_NAME} ALL
                       COMMENT "Checking Petalinux creation and hardware/kernel configuration"
                       DEPENDS ${PETALINUX_CONFIG_OUTPUT})

  else ()

    message (SEND_ERROR "petalinux_create: required parameter missing")

  endif ()

endfunction (petalinux_create)

################################ petalinux_app_create ####################################

function (petalinux_app_create ...)

  # set all keywords and their values to ""
  set (FUNCTION_KEYWORDS
       TARGET_NAME
       APP_NAME
       PROJ_NAME
       APP_TEMPLATE
       APP_SOURCES
       APP_BB)

  # reset local variables
  foreach(keyword ${FUNCTION_KEYWORDS})
    set (${keyword} "")
  endforeach(keyword)
  set (APP_TEMPLATE c)

  # parse input (could instead use cmake_parse_arguments)
  foreach (arg ${ARGV})
    list (FIND FUNCTION_KEYWORDS ${arg} ARGUMENT_IS_A_KEYWORD)
    if (${ARGUMENT_IS_A_KEYWORD} GREATER -1)
      set (CURRENT_PARAMETER ${arg})
      set (${CURRENT_PARAMETER} "")
    else (${ARGUMENT_IS_A_KEYWORD} GREATER -1)
      set (${CURRENT_PARAMETER} ${${CURRENT_PARAMETER}} ${arg})
    endif (${ARGUMENT_IS_A_KEYWORD} GREATER -1)
  endforeach (arg)

  if (PROJ_NAME AND APP_NAME AND APP_SOURCES)

    # If TARGET_NAME not specified, default is APP_NAME
    if (NOT TARGET_NAME)
      set (TARGET_NAME ${APP_NAME})
    endif ()

    if (APP_TEMPLATE STREQUAL "fpgamanager")
      # For fpgamanager, files are in recipes-firmware and it is assumed that APP_SOURCES specifies just one file (the BIT file),
      # which is copied and renamed to system.bit. This feature ("fpgamanager" template) is not currently used.
      set (APP_BIN           "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}/project-spec/meta-user/recipes-firmware/${APP_NAME}")
      set (APP_FILES_BIN     "${APP_BIN}/files/system.bit")
    else ()
      # For all other apps, files are in recipes-apps. In this case APP_SOURCES can specify more than one file, though further
      # updates may be necessary to support this (e.g., via .bbappend)
      set (APP_BIN           "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}/project-spec/meta-user/recipes-apps/${APP_NAME}")
      set (APP_FILES_BIN     "${APP_BIN}/files")
    endif ()

    set (APP_CREATE_OUTPUT "${APP_BIN}/${APP_NAME}.bb")

    set (CONFIG_ARCHIVE_DIR "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}-configs")
    set (ROOTFS_CONFIG      "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}/project-spec/configs/rootfs_config")

    if (APP_BB)

      add_custom_command (
          OUTPUT ${APP_CREATE_OUTPUT}
          COMMAND petalinux-create -p ${PROJ_NAME} -t apps --template ${APP_TEMPLATE} --name ${APP_NAME} --enable --force
          # Enabling the app modifies configs/rootfs_config, so archive it
          COMMAND ${CMAKE_COMMAND}
                  ARGS -E copy_if_different
                  ${ROOTFS_CONFIG}
                  "${CONFIG_ARCHIVE_DIR}/rootfs_config.${TARGET_NAME}"
          # Delete the autogenerated source file
          COMMAND ${CMAKE_COMMAND}
                  ARGS -E remove -f "${APP_FILES_BIN}/${APP_NAME}.c"
          # Copy the source files
          COMMAND ${CMAKE_COMMAND}
                  ARGS -E copy_if_different
                  ${APP_SOURCES}
                  ${APP_FILES_BIN}
          # Copy the bbappend source file
          COMMAND ${CMAKE_COMMAND}
                  ARGS -E copy_if_different
                  ${APP_BB}
                  ${APP_BIN}
          COMMENT "Creating ${TARGET_NAME}"
          DEPENDS ${PROJ_NAME} ${APP_SOURCES} ${APP_BB})

    else ()

      add_custom_command (
          OUTPUT ${APP_CREATE_OUTPUT}
          COMMAND petalinux-create -p ${PROJ_NAME} -t apps --template ${APP_TEMPLATE} --name ${APP_NAME} --enable --force
          # Enabling the app modifies configs/rootfs_config, so archive it
          COMMAND ${CMAKE_COMMAND}
                  ARGS -E copy_if_different
                  ${ROOTFS_CONFIG}
                  "${CONFIG_ARCHIVE_DIR}/rootfs_config.${TARGET_NAME}"
          # Copy the source files
          COMMAND ${CMAKE_COMMAND}
                  ARGS -E copy_if_different
                  ${APP_SOURCES}
                  ${APP_FILES_BIN}
          COMMENT "Creating ${TARGET_NAME}"
          DEPENDS ${PROJ_NAME} ${APP_SOURCES})

    endif ()

    add_custom_target (${TARGET_NAME} ALL
                       COMMENT "Checking creation of ${APP_NAME} app"
                       DEPENDS ${APP_CREATE_OUTPUT})

  else ()

    message (SEND_ERROR "petalinux_app_create: required parameter missing")

  endif ()

endfunction (petalinux_app_create)

################################ petalinux_build #########################################

function (petalinux_build ...)

  # set all keywords and their values to ""
  set (FUNCTION_KEYWORDS
       TARGET_NAME
       PROJ_NAME
       CONFIG_MENU
       ROOTFS_CONFIG_SRC
       BIT_FILE
       FSBL_FILE
       DEPENDENCIES)

  # reset local variables
  foreach(keyword ${FUNCTION_KEYWORDS})
    set (${keyword} "")
  endforeach(keyword)
  set (CONFIG_MENU OFF)

  # parse input (could instead use cmake_parse_arguments)
  foreach (arg ${ARGV})
    list (FIND FUNCTION_KEYWORDS ${arg} ARGUMENT_IS_A_KEYWORD)
    if (${ARGUMENT_IS_A_KEYWORD} GREATER -1)
      set (CURRENT_PARAMETER ${arg})
      set (${CURRENT_PARAMETER} "")
    else (${ARGUMENT_IS_A_KEYWORD} GREATER -1)
      set (${CURRENT_PARAMETER} ${${CURRENT_PARAMETER}} ${arg})
    endif (${ARGUMENT_IS_A_KEYWORD} GREATER -1)
  endforeach (arg)

  if (TARGET_NAME AND PROJ_NAME AND ROOTFS_CONFIG_SRC)

    if (CONFIG_MENU)
      set (CONFIG_OPTION "")
    else ()
      set (CONFIG_OPTION "--silentconfig")
    endif ()

    set (CONFIG_BIN_DIR   "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}/project-spec/configs")
    set (CONFIG_BIN_FILE  "${CONFIG_BIN_DIR}/rootfs_config")
    set (CONFIG_ARCHIVE_DIR "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}-configs")

    set (PETALINUX_IMAGE_DIR  "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}/images/linux")

    # First, configure rootfs. This is done here, rather than in petalinux_create, because
    # petalinux_app_create also modifies rootfs_config.

    # Output of petalinux-configure
    set (PETALINUX_ROOTFS_OUTPUT "${CONFIG_ARCHIVE_DIR}/rootfs_config.cfg")

    add_custom_command (
        OUTPUT ${PETALINUX_ROOTFS_OUTPUT}
        # Copy the rootfs_config file from the source tree.
        COMMAND ${CMAKE_COMMAND}
                ARGS -E copy_if_different
                ${ROOTFS_CONFIG_SRC}
                ${CONFIG_BIN_FILE}
        # Configure the rootfs. This does not take a long time and has one config menu
        # (shown if CONFIG_MENU is ON).
        COMMAND petalinux-config -p ${PROJ_NAME} -c rootfs ${CONFIG_OPTION}
        # Save in the archive (this is also the command output)
        COMMAND ${CMAKE_COMMAND}
                ARGS -E copy
                ${CONFIG_BIN_FILE}
                ${PETALINUX_ROOTFS_OUTPUT}
        # Update time of PETALINUX_ROOTFS_OUTPUT
        COMMAND ${CMAKE_COMMAND} -E touch ${PETALINUX_ROOTFS_OUTPUT}
        COMMENT "Copying rootfs_config to build tree and configuring rootfs"
        DEPENDS ${PROJ_NAME} ${ROOTFS_CONFIG_SRC})

    # Next, build petalinux.
    # Outputs of petalinux-build
    set (PETALINUX_IMAGE_UB   "${PETALINUX_IMAGE_DIR}/image.ub")
    set (PETALINUX_FSBL_FILE  "${PETALINUX_IMAGE_DIR}/zynq_fsbl.elf")
    set (PETALINUX_UBOOT_FILE "${PETALINUX_IMAGE_DIR}/u-boot.elf")
    if (FSBL_FILE)
      set (MSG_FSBL "custom FSBL")
    else ()
      set (FSBL_FILE ${PETALINUX_FSBL_FILE})
      set (MSG_FSBL "default FSBL")
    endif ()

    add_custom_command (
        OUTPUT ${PETALINUX_IMAGE_UB} ${PETALINUX_FSBL_FILE} ${PETALINUX_UBOOT_FILE}
        # petalinux-build does not automatically restore image.ub to the images/linux
        # directory, but it works if we first clean the kernel build and then rebuild.
        COMMAND petalinux-build -p ${PROJ_NAME} -c kernel -x clean
        COMMAND petalinux-build -p ${PROJ_NAME}
        # Update file times, just in case they were not regenerated
        COMMAND ${CMAKE_COMMAND} -E touch_nocreate ${PETALINUX_IMAGE_UB}
        COMMAND ${CMAKE_COMMAND} -E touch_nocreate ${PETALINUX_FSBL_FILE}
        COMMAND ${CMAKE_COMMAND} -E touch_nocreate ${PETALINUX_UBOOT_FILE}
        COMMENT "Building petalinux"
        DEPENDS ${PETALINUX_ROOTFS_OUTPUT} ${DEPENDENCIES})

    # Package the boot files

    # Output of petalinux-package
    set (PETALINUX_BOOT_FILE  "${PETALINUX_IMAGE_DIR}/BOOT.bin")

    if (BIT_FILE)

      add_custom_command (
          OUTPUT ${PETALINUX_BOOT_FILE}
          COMMAND petalinux-package -p ${PROJ_NAME} --boot --fsbl ${PETALINUX_FSBL_FILE} --fpga ${BIT_FILE}
                                    --u-boot ${PETALINUX_UBOOT_FILE} --force -o ${PETALINUX_BOOT_FILE}
          COMMENT "Petalinux package (boot image), ${MSG_FSBL}"
          DEPENDS ${BIT_FILE} ${PETALINUX_IMAGE_UB} ${PETALINUX_FSBL_FILE} ${PETALINUX_UBOOT_FILE})

    else ()

      add_custom_command (
          OUTPUT ${PETALINUX_BOOT_FILE}
          COMMAND petalinux-package -p ${PROJ_NAME} --boot --fsbl ${PETALINUX_FSBL_FILE}
                                    --u-boot ${PETALINUX_UBOOT_FILE} --force -o ${PETALINUX_BOOT_FILE}
          COMMENT "Petalinux package (boot image) without BIT file, ${MSG_FSBL}"
                "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}/project-spec/configs"
          DEPENDS ${PETALINUX_IMAGE_UB} ${PETALINUX_FSBL_FILE} ${PETALINUX_UBOOT_FILE})

    endif ()

    add_custom_target (${TARGET_NAME} ALL
                       COMMENT "Checking Petalinux rootfs configuration and build"
                       DEPENDS ${PETALINUX_BOOT_FILE})

    # Finally, check if config files in build tree differ from source tree
    # (if there is a difference, consider updating source tree).
    get_filename_component (CONFIG_SRC_DIR ${ROOTFS_CONFIG_SRC} DIRECTORY)
    add_custom_command (
        TARGET ${TARGET_NAME} POST_BUILD
        # CMake provides a portable compare_files command:
        #   ${CMAKE_COMMAND} ARGS -E compare_files <file1> <file2>
        # But, since Petalinux only runs on Linux and since we also want
        # to see which lines are different, we just use the "diff" command.
        # The "|| :" is added so that differences are not flagged as errors.
        COMMAND ${CMAKE_COMMAND} -E echo "Checking config"
        COMMAND diff
                "${CONFIG_BIN_DIR}/config"
                "${CONFIG_SRC_DIR}/config"
                || :
        COMMAND ${CMAKE_COMMAND} -E echo "Checking rootfs_config"
        COMMAND diff
                "${CONFIG_BIN_DIR}/rootfs_config"
                ${ROOTFS_CONFIG_SRC}
                || :
        COMMENT "Comparing config files in build tree to source tree")

  else ()

    message (SEND_ERROR "petalinux_build: required parameter missing")

  endif ()

endfunction (petalinux_build)

function (petalinux_build_sdk)

  # set all keywords and their values to ""
  set (FUNCTION_KEYWORDS
       TARGET_NAME
       PROJ_NAME
       SDK_INSTALL_DIR
       SYSROOT_ZIP
       DEPENDENCIES)

  # reset local variables
  foreach(keyword ${FUNCTION_KEYWORDS})
    set (${keyword} "")
  endforeach(keyword)

  # parse input (could instead use cmake_parse_arguments)
  foreach (arg ${ARGV})
    list (FIND FUNCTION_KEYWORDS ${arg} ARGUMENT_IS_A_KEYWORD)
    if (${ARGUMENT_IS_A_KEYWORD} GREATER -1)
      set (CURRENT_PARAMETER ${arg})
      set (${CURRENT_PARAMETER} "")
    else (${ARGUMENT_IS_A_KEYWORD} GREATER -1)
      set (${CURRENT_PARAMETER} ${${CURRENT_PARAMETER}} ${arg})
    endif (${ARGUMENT_IS_A_KEYWORD} GREATER -1)
  endforeach (arg)

  if (TARGET_NAME AND PROJ_NAME AND SDK_INSTALL_DIR AND SYSROOT_ZIP)

    # Assume that SDK (sdk.sh) needs to be regenerated if image.ub has been updated
    set (PETALINUX_IMAGE_DIR  "${CMAKE_CURRENT_BINARY_DIR}/${PROJ_NAME}/images/linux")
    set (PETALINUX_IMAGE_UB   "${PETALINUX_IMAGE_DIR}/image.ub")
    set (PETALINUX_SH_FILE    "${PETALINUX_IMAGE_DIR}/sdk.sh")
    set (PETALINUX_SDK_FILE   "${SDK_INSTALL_DIR}/cmake.sdk")
    set (PETALINUX_SYSROOT_DIR "cortexa9t2hf-neon-xilinx-linux-gnueabi")

    add_custom_command (
        OUTPUT ${PETALINUX_SH_FILE}
        COMMAND petalinux-build --sdk
        # Set WORKING_DIRECTORY to Petalinux project subdirectory
        WORKING_DIRECTORY ${PROJ_NAME}
        COMMENT "Building Petalinux SDK"
        DEPENDS ${PETALINUX_IMAGE_UB})

    add_custom_command (
        OUTPUT ${PETALINUX_SDK_FILE}
        COMMAND petalinux-package --sysroot -d ${SDK_INSTALL_DIR}
        COMMAND ${CMAKE_COMMAND} -E touch ${PETALINUX_SDK_FILE}
        # For some reason, petalinux-package does not accept the -p parameter,
        # so we use WORKING_DIRECTORY to specify the project subdirectory
        WORKING_DIRECTORY ${PROJ_NAME}
        COMMENT "Packaging Petalinux SDK"
        DEPENDS ${PETALINUX_SH_FILE})

    get_filename_component (SYSROOT_ZIP_FILE ${SYSROOT_ZIP} NAME)

    # Manually remove some debug files to reduce file size. There may be a better way to do this.
    # The "|| :" is added to ignore zip errors
    add_custom_command (
        OUTPUT ${SYSROOT_ZIP}
        COMMAND ${CMAKE_COMMAND} -E remove -f ${SYSROOT_ZIP}
        COMMAND zip -9 -q -r ${SYSROOT_ZIP}
                    ${PETALINUX_SYSROOT_DIR}
                    -x \"${PETALINUX_SYSROOT_DIR}/usr/src/debug/*\" \"\*/.debug/*\" @
                || :
        WORKING_DIRECTORY "${SDK_INSTALL_DIR}/sysroots"
        COMMENT "Creating ${SYSROOT_ZIP_FILE} from ${PETALINUX_SYSROOT_DIR}"
        DEPENDS ${PETALINUX_SDK_FILE})

    add_custom_target (${TARGET_NAME} ALL
                       COMMENT "Checking Petalinux SDK"
                       DEPENDS ${SYSROOT_ZIP} ${DEPENDENCIES})

    set_property(TARGET ${TARGET_NAME}
                        PROPERTY OUTPUT_NAME ${PETALINUX_SDK_FILE})

  else ()

    message (SEND_ERROR "petalinux_build_sdk: required parameter missing")

  endif ()

endfunction (petalinux_build_sdk)
